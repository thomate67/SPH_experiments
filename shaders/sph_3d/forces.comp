#version 460

layout (local_size_x = 128, local_size_y = 1, local_size_z = 1 ) in;

layout(std430, binding = 0) buffer positions
{
    vec4 position[];
};

layout(std430, binding = 1) buffer velocities
{
    vec4 velocity[];
};

layout(std430, binding = 2) buffer forces
{
    vec4 force[];
};

layout(std430, binding = 3) buffer densities
{
    float density[];
};

layout(std430, binding = 4) buffer pressures
{
    float pressure[];
};

// constants
#define PI_FLOAT 3.1415927410125732421875f

// OpenGL y-axis is pointing up, while Vulkan y-axis is pointing down.
// So in OpenGL this is negative, but in Vulkan this is positive.
#define GRAVITY_FORCE vec3(0, -9806.65, 0)

// uniform variables
uniform int NUM_PARTICLES;
uniform float PARTICLE_MASS;
uniform float PARTICLE_VISCOSITY;
uniform float SMOOTHING_LENGTH;

void main()
{
    uint i = gl_GlobalInvocationID.x;  
    // compute all forces
    vec3 pressure_force = vec3(0, 0, 0);
    vec3 viscosity_force = vec3(0, 0, 0);
    
    for (uint j = 0; j < NUM_PARTICLES; j++)
    {
        if (i == j)
        {
            continue;
        }
        vec3 delta = vec3(position[i] - position[j]);
        float r = length(delta);
        if (r < SMOOTHING_LENGTH)
        {
            vec3 spikyKernel = -45.f / (PI_FLOAT * pow(SMOOTHING_LENGTH, 6)) * pow(SMOOTHING_LENGTH - r, 2) * normalize(delta);
            pressure_force -= PARTICLE_MASS * (pressure[i] + pressure[j]) / (2.f * density[j]) * spikyKernel;
            // gradient of spiky kernel
            
            float viscosityKernel = 45.f / (PI_FLOAT * pow(SMOOTHING_LENGTH, 6)) * (SMOOTHING_LENGTH - r);
            viscosity_force += PARTICLE_MASS * vec3(velocity[j] - velocity[i]) / density[j] * viscosityKernel;
            // Laplacian of viscosity kernel
                
        }
    }
    viscosity_force *= PARTICLE_VISCOSITY;
    vec3 external_force = density[i] * GRAVITY_FORCE;

    force[i].xyz = pressure_force + viscosity_force + external_force;
}